//////////////////////////////////////////
//                                      //
//     ctrl_placer.mel - mel script     //
//                                      //
//////////////////////////////////////////
//
// Copyright ï¿½2021 Xavier Magher
//
// DESCRIPTION:	 
//    quicly setup controllers located in the ctrl_placer_plugin folder
//
//////////////////////////////////////////////////////////////////////



if (`window -exists XMctrl`){
    deleteUI XMctrl;
}


global string $XMCtrlbuffer;
global string $XMCtrlcont;
global string $XMCtrlrename;
global string $XMCtrlgroup;

// add the nomenclature UI, 
// allows other proc to use its textfield
global proc addNumUI(){
    global string $XMCtrlbuffer;
    global string $XMCtrlcont;
    
    string $bufferNUM = "_ctrl_srtBuffer";
    string $contNUM = "_ctrl";
    
    columnLayout;
    text -l "buffer:" -w 150 -h 25 -fn "boldLabelFont"  -al left ;
    $XMCtrlbuffer = `textFieldGrp //-tcc "BufferUpdate()"
                                -columnWidth 1 0
                                -columnWidth 2 150
                                -text $bufferNUM -label "buffer"`;
    text -l "controller:" -al left -fn "boldLabelFont";
    $XMCtrlcont = `textFieldGrp //-tcc "BufferUpdate()"
                                -columnWidth 1 0
                                -columnWidth 2 150
                                -text $contNUM -label "cont"`;
                                
}

global proc addRenameBoxUI(){
    global string $XMCtrlrename;
    global string $XMCtrlgroup;
     string $XMCtrlrename = `checkBox -v true -ann "rename controllers to selected object" -l "rename ctrl"`;
     string $XMCtrlgroup = `checkBox -v true -ann "put controller inside of a group" -l "group ctrl"`;
}

//get the script directories
global proc string getMelFld(int $x){
    string $scriptfld = `internalVar -userScriptDir`;
    string $dir = $scriptfld + "/XM_ctrl_placer_plugin/";
    string $ImgFld = $scriptfld + "/XM_ctrl_placer_plugin/ctrl_img/";
    string $MelFld = $scriptfld + "/XM_ctrl_placer_plugin/ctrl_mel/";
    switch ($x){
        case 0:
        return $dir;
        break;
        
        case 1:
        return $ImgFld;
        break;
        
        case 2:
        return $MelFld;
        break;
    }
}

// get the file in the script directory
global proc string[] getCtrlFile(){
    string $ImgFld = getMelFld(1);
    string $MelFld = getMelFld(2);
    string $CtrlFile[] = `getFileList -fld $MelFld`;
    return $CtrlFile;
}


global proc string GetLongName(string $name){
    string $long[] = `ls -l $name`;
    return $long[0];
}

global proc int GetHNumber(string $name){
    string $longname[] = ls("-l", $name);
    string $tokens[];
    tokenize($longname[0], "|", $tokens);
    return size($tokens);
}

// import and sets up the controllers
global proc AddCtrl(string $button)
{
	string $label = `iconTextButton -q -l $button`;
	print($button);
    string $MelFld = getMelFld(2);
    string $CtrlFile[] = getCtrlFile();
    global string $XMCtrlbuffer;
    global string $XMCtrlcont;
    global string $XMCtrlrename;
    global string $XMCtrlgroup;
    string $b = `textFieldGrp -q -text $XMCtrlbuffer`;
    string $c = `textFieldGrp -q -text $XMCtrlcont`;
    string $r = `checkBox -q -v $XMCtrlrename`;
    string $g = `checkBox -q -v $XMCtrlgroup`;

	string $selected[] = `ls -sl`;
    select -r $selected;
    //if nothing is selected simply import the controller
    if(`size($selected)` == 0){
        if($g == 0){
        file -import -type "mayaBinary" -mnc true  ($MelFld + $label);
        }
        else{
            print($label);
            file -import -type "mayaBinary" -gr -gn $label -mnc true  ($MelFld + $label);
            }
    }
    
    
    for($n = 0; $n < (`size($selected)`) ; $n++){
    if($g == 0){    
	    file -import -type "mayaBinary" -mnc true -ns "Imported" ($MelFld + $label);
	}
	else{
	    file -import -type "mayaBinary" -gr -gn "Imported:" -mnc true -ns "Imported" ($MelFld + $label);    
	}
	select "Imported:*";
	string $ss[] = `ls -sl -tr`;
	for ($s = 0; $s < (`size($ss)`); $s++){
	    
	    // check if specific object is a root
	    if(GetHNumber(GetLongName($ss[$s])) <= 1){
	        matchTransform -piv -pos -rot (GetLongName($ss[$s])) $selected[$n];
	    string $newbuffer = $selected[$n] + $b;
	    if($r == 0){
	        $newbuffer = substituteAllString($ss[$s], "Imported", "");
	    }
	        
	    rename $ss[$s] $newbuffer;
	
	    }
	    else{
	                
	    string $newcont = $selected[$n] + $c;
	    if($r == 0){
	        $newcont = substituteAllString($ss[$s], "Imported", "");
	    }
	    //print(`ls -l $ss[$s]`);
	    rename $ss[$s] $newcont;
	            
	    }
	}
	
	
	select -r $selected;

	}
}

//search function
global proc search(string $bList[], string $search){
    string $s =`textFieldGrp -q -text $search`;
    string $sL = `tolower $s`;
    
    for($b = 0; $b < (`size($bList)`) ; $b++){
        string $button = `iconTextButton -q -l $bList[$b]`;
        string $buttonL = `tolower $button`;
        int $vis = 0;
        if(size(`match $sL $buttonL`) >= 1){
            $vis = 1;
        }
        if(size($s) == 0){
            $vis = 1;
        }
         iconTextButton -e -en $vis $bList[$b]; 
    }
}


string $MelFld = getMelFld(2);
string $CtrlFile[] = `getFileList -fld $MelFld`;
string $getButton[];

global proc InfoPage()
{
    system "load https://github.com/Redfill/XMController" ;
}

//windows
window -title "XM crtl creator" -widthHeight 100 100 XMctrl;

string $cH1 = `columnLayout -adj true  `;
//top menu
 menuBarLayout;
    menu -l "File";
        string $dir = getMelFld(0);
        menuItem -l "open file location" -c ("launch -dir $dir");
        menuItem -l "Exit" -c "deleteUI XMctrl";
    menu -l "help";
        menuItem -l "product page" -c ("InfoPage()");
 //ui to set the nomanclature of the controllers
 frameLayout -l "nomenclature" -cll true -cl true -w 150 -bgc .2 .2 .3;

 addNumUI();
 
 //ui for controller placment
 setParent $cH1; 
 frameLayout -w 310 -l "create controller" -cll true -bgc .5 .2 .2 ;
 
 rowLayout -nc 3 ;
 string $searchCtrl = `textFieldGrp -ann "search for a controller"  -cw 1 40 -l "search:" -cc "search($getButton, $searchCtrl)"`;
 addRenameBoxUI();
 //grid of buttons
 setParent ..;
 gridLayout -nc 3 -cwh 150 150 ;
 
 //goes threw the plugin folder and add the buttons
 for($n = 0; $n < (`size($CtrlFile)`) ; $n++){
    string $butt = `iconTextButton -l ($CtrlFile[$n])`;
    string $PNGFile = `substitute "mb" $CtrlFile[$n] "png"`;
    $getButton[$n] = $butt;
	iconTextButton -style "iconOnly" -i (`getMelFld(1)` + $PNGFile) -ann $CtrlFile[$n] -e -c ("AddCtrl "+$butt) $butt;
 }


 

 
 
 showWindow XMctrl;
 window -e -wh 455 500 XMctrl;
 